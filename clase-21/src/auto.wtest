import auto.*
import calle.*
import autoAEsquivar.*
import wollok.game.*

describe "movimientos del auto" {
	const cantidadCarriles = calle.cantidadDeCarriles()
	
	method initialize() {
		calle.positionX(1)

		auto.ubicarteParaEmpezar()
	}
	
	test "la posicion inicial del auto es a la mitad de la calle" {
		assert.equals(4, auto.position().x())
	}

	test "si se mueve a la derecha, aumenta su X en 1" {
		auto.moveteADerecha()
		
		assert.equals(5, auto.position().x())
	}
	
	test "si se mueve a la izquierda, disminuye su X en 1" {
		auto.moveteAIzquierda()

		assert.equals(3, auto.position().x())
	}

	test "no se puede salir de la calle por la izquierda" {
		cantidadCarriles.times { n => 
			auto.moveteAIzquierda()
		}
		
		assert.equals(1, auto.position().x())
	}
	
	test "no se puede salir de la calle por derecha" {
		cantidadCarriles.times { n => 
			auto.moveteADerecha()
		}
		
		assert.equals(7, auto.position().x())
	}
}

describe "colisiones del auto" {
	test "el auto empieza con 3 vidas" {
		assert.equals(3, auto.vidas())
	}

	test "si choca con un auto a esquivar, se pierde una vida" {
		auto.chocaCon(new AutoAEsquivar(position = new Position(x=0,y=0)))
		
		assert.equals(2, auto.vidas())
	}
	
	test "si choca con una seccion de la calle no pasa nada" {
		auto.chocaCon(new SeccionDeCalle())
		
		assert.equals(3, auto.vidas())
	}
}